// fxGetErrorInfo()
// Â© 2023 Adiv Abramson

// Analyzes the table column specified and generates an output table if any errors are detected.
// Output table has the following structure: [Column Name], [Reason], [Message], [Detail]
// Optional parameter source_column will populate the [Column Name] column of the output table.
// Otherwise that column will be populated by default with "My Data Column"

// 



(Source as list, optional source_column as text) =>
let
    Get_Source_Column_Name = if source_column = null then "My Data Column" else source_column,
    Test_Values = List.Generate(
                                ()=> 0,
                                each _ < List.Count(Source),
                                each _ + 1,
                                each 
                                    // Create a list of text items that includes error reason, error message, and the value
                                    // that raises an error cast as a text value. If a particular value in the column
                                    // doesn't raise an error, return null.
                                    let 
                                        Err_Source = try Source{_},
                                        Err_Info = if (Err_Source)[HasError] then  
                                                      {
                                                        (Err_Source)[Error][Reason], 
                                                        (Err_Source)[Error][Message], 
                                                        Text.From((Err_Source)[Error][Detail])
                                                        } 
                                                   else 
                                                    null 
                                    in 
                                        Err_Info
                            ),
    // Select the non null items in the list generated by the previous step.
    Error_List = List.Select(Test_Values, each _ <> null),

    // Convert the list of non null items to a table and add a column with the 
    // source column name specified or a generic default name.
    // If the table is empty, return null.
    Error_Table = Table.FromColumns(Error_List),
    Final_Table = if Table.RowCount(Error_Table) > 0 then  
                    let Table_Source = Table.Transpose(Error_Table, {"Reason", "Message", "Detail"}),
                        Add_Name_Of_Column = Table.AddColumn(Table_Source, "Column Name", each Get_Source_Column_Name),
                        Reorder_Table_Columns = Table.ReorderColumns(Add_Name_Of_Column, {"Column Name", "Reason", "Message", "Detail"})
                    in 
                        Reorder_Table_Columns
                else 
                    null
    
in
    Final_Table